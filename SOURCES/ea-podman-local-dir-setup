#!/usr/local/cpanel/3rdparty/bin/perl
# cpanel - ea-podman-local-dir-setup                 Copyright 2022 cPanel, Inc.
#                                                           All rights Reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited

use strict;
use warnings;

package scripts::ea_podman_local_dir_setup;

use File::Path::Tiny ();    # Path::Tiny’s version is overly complicated
use Path::Tiny 'path';
use XML::LibXML ();
use Cwd         ();

run(@ARGV) if !caller;

sub run {
    my ( $host_path, @ports ) = @_;

    die "Must not have ports ($host_path)\n" if @ports;

    return add( $host_path );
}

################
#### commands ##
################

sub add {
    my ( $host_path ) = @_;

    my ( $user, $homedir ) = ( getpwuid($>) )[ 0, 7 ];

    _bail("$host_path appears to have setup already; aborting so we don’t break anything") if -d $host_path && !_memcached_dir_is_empty ($host_path);

    print "Adding a memcached latest instance for “$user” …\n";

    my $curdir = Cwd::cwd();

    {
        # the memcached container requires a wide open permission set for the
        # dir :(

        my $orig_umask = umask(0777);

        mkdir $host_path;    # it may exist so don’t check it’s RV
        die "Could not create directory “$host_path”" if !-d $host_path;
        chmod 0777, $host_path;
        chdir $host_path or die "Could not change into “$host_path”: $!\n";
        mkdir "socket_dir";
        chmod 0777, "socket_dir";

        umask($orig_umask);
    }

    chdir $curdir or warn "Could not chdir back to “$curdir”: $!\n";
    print " … done!\n";

    return;
}

###############
#### helpers ##
###############

sub _bail {
    my ($msg) = @_;

    chomp($msg);
    warn "$msg\n";

    exit(1);    # there is no return()ing from this lol
}

sub _memcached_dir_is_empty {
    my ($dir) = @_;
    my @contents = path($dir)->children;
    return 1 if !@contents;
    return;
}

1;
